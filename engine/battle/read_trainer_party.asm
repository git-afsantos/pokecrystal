ReadPlayerParty:
    ld hl, wPartyCount  ; hl = wPartyCount (address)
	xor a               ; a = 0
	ld [hli], a         ; *hl = 0; hl++ (hl = wPartySpecies)
	dec a               ; a = -1
	ld [hl], a          ; *hl = -1 (end of list)

	ld hl, wPartyMons
	ld bc, wPartyMonsEnd - wPartyMons
	xor a
	call ByteFill ; fills bc bytes (all party structs) with a (0)

	ld a, [wScriptVar] ; Trainer Class constant

	dec a ; zero-based index
	ld c, a
	ld b, 0
	ld hl, PlayerTrainerGroups ; go to table of addresses
	add hl, bc
	add hl, bc  ; add twice to skip words, not bytes
	ld a, [hli] ; save the low byte of the address
	ld h, [hl]  ; high part of the address remains
	ld l, a     ; set the low part of the address to the concrete trainer

.skip_name
	ld a, [hli]
	cp "@"
	jr nz, .skip_name

	ld d, h
	ld e, l
	ld hl, PlayerTrainerType2
	ld bc, .done
	push bc
	jp hl

.done
	ret

PlayerTrainerGroups:
; entries correspond to trainer classes (see constants/trainer_constants.asm)
	dw PlayerFalknerGroup

PlayerFalknerGroup:
	; FALKNER (1)
	db "FALKNER@"
	db 50, PIDGEOT,  FLY, MIRROR_MOVE, AGILITY, QUICK_ATTACK
	db 50, FEAROW,   DRILL_PECK, AGILITY, DOUBLE_EDGE, PURSUIT
	db 50, NOCTOWL,  HYPNOSIS, REFLECT, NIGHT_SHADE, REST
	db 50, MURKROW,  DRILL_PECK, FAINT_ATTACK, PURSUIT, NIGHT_SHADE
	db 50, SKARMORY, WHIRLWIND, DRILL_PECK, REST, CURSE
	db 50, PIDGEOT,  RETURN, WING_ATTACK, STEEL_WING, MIRROR_MOVE
	db 50, FEAROW,   REST, SLEEP_TALK, DOUBLE_EDGE, DRILL_PECK
	db 50, DODRIO,   DOUBLE_EDGE, DRILL_PECK, REST, SLEEP_TALK
	db -1 ; end

PlayerTrainerType2:
; level, species, moves
	ld h, d
	ld l, e
.loop
	ld a, [hli]
	cp $ff
	ret z

	ld [wCurPartyLevel], a
	ld a, [hli]
	ld [wCurPartySpecies], a
	ld a, PARTYMON
	ld [wMonType], a
	;ld a, TRAINER_BATTLE
	;ld [wBattleMode], a

; TryAddMonToParty requires
; [x] wPartyCount
; [x] wMonType (PARTYMON)
; [x] wCurPartySpecies
; [?] wPartyMonOT
; [?] wPlayerName
; [ ] wBattleMode
; [x] wCurPartyLevel
	push hl
	predef TryAddMonToParty
	ld a, [wPartyCount]
	dec a
	ld hl, wPartyMon1Moves
	ld bc, PARTYMON_STRUCT_LENGTH
	call AddNTimes
	ld d, h
	ld e, l
	pop hl

	ld b, NUM_MOVES
.copy_moves
	ld a, [hli]
	ld [de], a
	inc de
	dec b
	jr nz, .copy_moves

	push hl

	ld a, [wPartyCount]
	dec a
	ld hl, wPartyMon1Species
	ld bc, PARTYMON_STRUCT_LENGTH
	call AddNTimes
	ld d, h
	ld e, l
	ld hl, MON_PP
	add hl, de
	push hl
	ld hl, MON_MOVES
	add hl, de
	pop de

	ld b, NUM_MOVES
.copy_pp
	ld a, [hli]
	and a
	jr z, .copied_pp

	push hl
	push bc
	dec a
	ld hl, Moves + MOVE_PP
	ld bc, MOVE_LENGTH
	call AddNTimes
	ld a, BANK(Moves)
	call GetFarByte
	pop bc
	pop hl

	ld [de], a
	inc de
	dec b
	jr nz, .copy_pp
.copied_pp

	pop hl
	jr .loop


ReadTrainerParty:
	ld a, [wInBattleTowerBattle]
	bit 0, a
	ret nz

	ld a, [wLinkMode]
	and a
	ret nz

	ld hl, wOTPartyCount
	xor a
	ld [hli], a
	dec a
	ld [hl], a

	ld hl, wOTPartyMons
	ld bc, wOTPartyMonsEnd - wOTPartyMons
	xor a
	call ByteFill

	ld a, [wOtherTrainerClass]

	dec a
	ld c, a
	ld b, 0
	ld hl, TrainerGroups
	add hl, bc
	add hl, bc
	ld a, [hli]
	ld h, [hl]
	ld l, a

	ld a, [wOtherTrainerID]
	ld b, a
.skip_trainer
	dec b
	jr z, .got_trainer
.loop
	ld a, [hli]
	cp -1
	jr nz, .loop
	jr .skip_trainer
.got_trainer

.skip_name
	ld a, [hli]
	cp "@"
	jr nz, .skip_name

	ld d, h
	ld e, l
	ld hl, TrainerType4
	ld bc, .done
	push bc
	jp hl

.done
	jp ComputeTrainerReward


TrainerType4:
; level, species, item, moves
	ld h, d
	ld l, e
.loop
	ld a, [hli]
	cp $ff
	ret z

	ld [wCurPartyLevel], a
	ld a, [hli]
	ld [wCurPartySpecies], a

	ld a, OTPARTYMON
	ld [wMonType], a

	push hl
	predef TryAddMonToParty
	ld a, [wOTPartyCount]
	dec a
	ld hl, wOTPartyMon1Item
	ld bc, PARTYMON_STRUCT_LENGTH
	call AddNTimes
	ld d, h
	ld e, l
	pop hl

	ld a, [hli]
	ld [de], a

	push hl
	ld a, [wOTPartyCount]
	dec a
	ld hl, wOTPartyMon1Moves
	ld bc, PARTYMON_STRUCT_LENGTH
	call AddNTimes
	ld d, h
	ld e, l
	pop hl

	ld b, NUM_MOVES
.copy_moves
	ld a, [hli]
	ld [de], a
	inc de
	dec b
	jr nz, .copy_moves

	push hl

	ld a, [wOTPartyCount]
	dec a
	ld hl, wOTPartyMon1
	ld bc, PARTYMON_STRUCT_LENGTH
	call AddNTimes
	ld d, h
	ld e, l
	ld hl, MON_PP
	add hl, de

	push hl
	ld hl, MON_MOVES
	add hl, de
	pop de

	ld b, NUM_MOVES
.copy_pp
	ld a, [hli]
	and a
	jr z, .copied_pp

	push hl
	push bc
	dec a
	ld hl, Moves + MOVE_PP
	ld bc, MOVE_LENGTH
	call AddNTimes
	ld a, BANK(Moves)
	call GetFarByte
	pop bc
	pop hl

	ld [de], a
	inc de
	dec b
	jr nz, .copy_pp
.copied_pp

	pop hl
	jr .loop

ComputeTrainerReward:
	ld hl, hProduct
	xor a
	ld [hli], a
	ld [hli], a ; hMultiplicand + 0
	ld [hli], a ; hMultiplicand + 1
	ld a, [wEnemyTrainerBaseReward]
	ld [hli], a ; hMultiplicand + 2
	ld a, [wCurPartyLevel]
	ld [hl], a ; hMultiplier
	call Multiply
	ld hl, wBattleReward
	xor a
	ld [hli], a
	ldh a, [hProduct + 2]
	ld [hli], a
	ldh a, [hProduct + 3]
	ld [hl], a
	ret

Battle_GetTrainerName::
	ld a, [wInBattleTowerBattle]
	bit 0, a
	ld hl, wOTPlayerName
	jp nz, CopyTrainerName

	ld a, [wOtherTrainerID]
	ld b, a
	ld a, [wOtherTrainerClass]
	ld c, a

GetTrainerName::
	ld a, c
	cp CAL
	jr nz, .not_cal2

	ld a, BANK(sMysteryGiftTrainerHouseFlag)
	call OpenSRAM
	ld a, [sMysteryGiftTrainerHouseFlag]
	and a
	call CloseSRAM
	jr z, .not_cal2

	ld a, BANK(sMysteryGiftPartnerName)
	call OpenSRAM
	ld hl, sMysteryGiftPartnerName
	call CopyTrainerName
	jp CloseSRAM

.not_cal2
	dec c
	push bc
	ld b, 0
	ld hl, TrainerGroups
	add hl, bc
	add hl, bc
	ld a, [hli]
	ld h, [hl]
	ld l, a
	pop bc

.loop
	dec b
	jr z, CopyTrainerName

.skip
	ld a, [hli]
	cp $ff
	jr nz, .skip
	jr .loop

CopyTrainerName:
	ld de, wStringBuffer1
	push de
	ld bc, NAME_LENGTH
	call CopyBytes
	pop de
	ret

IncompleteCopyNameFunction: ; unreferenced
; Copy of CopyTrainerName but without "call CopyBytes"
	ld de, wStringBuffer1
	push de
	ld bc, NAME_LENGTH
	pop de
	ret

INCLUDE "data/trainers/parties.asm"
